#!/usr/bin/env zsh
# Copyright 2018 Jamiel Almeida
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Install systemd units for user services, et al.

setopt err_exit
setopt no_unset
setopt pipe_fail

typeset -A user_units=()

user_units[${HOME}/.config/systemd/user/audio.service]="$(
command -v start-pulseaudio-x11 >/dev/null 2>&1 && \
command -v volume-defaults >/dev/null 2>&1 && \
cat <<-EOUNITFILE || true
[Unit]
Description=Set up audio correctly
PartOf=graphical-session.target

[Service]
Type=oneshot
ExecStart=${${:-start-pulseaudio-x11}:c:A}
ExecStartPost=${${:-volume-defaults}:c:A}
Environment=DISPLAY=:0

[Install]
WantedBy=xsession.target
EOUNITFILE
)"

user_units[${HOME}/.config/systemd/user/battery-monitor.service]="$(
command -v monitor-battery >/dev/null 2>&1 && \
cat <<-EOUNITFILE || true
[Unit]
Description=Script that monitors battery charge
PartOf=graphical-session.target

[Service]
ExecStart=${${:-monitor-battery}:c:A} -w 30 -c 10 -b 5 -n 1800
Environment=DISPLAY=:0
Restart=on-failure

[Install]
WantedBy=xsession.target
EOUNITFILE
)"

user_units[${HOME}/.config/systemd/user/pointer-prefs.service]="$(
command -v x11-set-mouse-prefs >/dev/null 2>&1 && \
command -v synclient >/dev/null 2>&1 && \
cat <<-EOUNITFILE || true
[Unit]
Description=Set my common mouse preferences for this x11 session
PartOf=graphical-session.target

[Service]
Type=oneshot
ExecStart=${${:-x11-set-mouse-prefs}:c:A}
ExecStartPost=${${:-synclient}:c:A} TouchpadOff=1
Environment=DISPLAY=:0

[Install]
WantedBy=xsession.target
EOUNITFILE
)"

user_units[${HOME}/.config/systemd/user/xresources.service]="$(
command -v xset >/dev/null 2>&1 && \
command -v x11-replace-xdefaults >/dev/null 2>&1 && \
cat <<-EOUNITFILE || true
[Unit]
Description=Reload xdefaults for this x11 session
PartOf=graphical-session.target

[Service]
Type=oneshot
ExecStartPre=${${:-xset}:c:A} -b
ExecStartPre=${${:-xset}:c:A} dpms 610 0 0
ExecStartPre=${${:-xset}:c:A} s 540 60
ExecStart=${${:-x11-replace-xdefaults}:c:A}
Environment=DISPLAY=:0

[Install]
WantedBy=xsession.target
EOUNITFILE
)"

typeset did_something=
for target_file in "${(k)user_units[@]}"; do
    [[ -n "${user_units[${target_file}]:-}" ]] || continue
    [[ ! -r "${target_file}" ]] || continue
    install --verbose --mode='u=rw,go=' /dev/null "${target_file}"
    tee "${target_file}" <<<"${user_units[${target_file}]:-}"
    did_something='yes'
done

if [[ "${did_something}" == 'yes' ]]; then
    systemctl --user daemon-reload
fi
