# Copyright 2018 Jamiel Almeida
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Show a list of processes filtered down by patterns provided as arguments, if
# none are present, show the process tree for the user

local -a pats=( "${@}" )
local my_pid="${$}"
local ps_cmd="ps -u ${USER} -o pid,ppid,etime,args"
local grep_colors=(
    "sl=0;00"
    "cx=0;90"
    "rv"
    "mt=1;33"
    "fn=0;34"
    "ln=0;33"
    "se=1;30"
)
[[ "${#pats[@]}" -ne 0 ]] || ps_cmd="${ps_cmd} --forest"
local process_list="$( ${(z)ps_cmd} )"
local without_this_pid="$(
    awk 'NR != 1 && $1 != '${my_pid}' && $2 != '${my_pid}' {print}' <<<"${process_list}"
)"
if [[ "${#pats[@]}" -ne 0 ]]; then
    sed -n -e 1p <<<"${process_list}"
    echo "${without_this_pid}" | \
        env  \
            GREP_COLORS="${(pj.:.)grep_colors}" \
            grep --color=always -i -F "${(F)pats}"
else
    sed -n -e 1p <<<"${process_list}"
    echo "${without_this_pid}"
fi
