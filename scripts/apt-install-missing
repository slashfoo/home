#!/usr/bin/env zsh
# Copyright 2018 Jamiel Almeida
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Wrapper around apt to install packages that are not installed from a list,
# and if none were missing return zero without prompting

setopt err_exit
setopt no_unset
setopt pipe_fail

local -a reqs=( "${@}" )
local -a installed=( $(apt-installed) )

local -a missing=()
for req in "${(@)reqs}"; do
    [[ "${installed[(i)${req}]}" -gt "${#installed[@]}" ]] || continue
    missing+=( "${req}" )
done
if [[ "${#missing[@]}" -eq 0 ]]; then
    exit 0
fi

echo "Missing packages:" >&2
echo "${(F)missing[@]}" | nl >&2
echo "Attempting to install requirements" >&2
set -- "${(@)missing}"
exec sudo apt install --no-install-recommends "${@}"
