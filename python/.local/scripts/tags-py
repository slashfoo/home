#!/usr/bin/env zsh
# Copyright 2018 Jamiel Almeida
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Generate tags for a python project and all the python files it'd be able to
# import

setopt err_exit
setopt no_unset
setopt pipe_fail
setopt extended_glob
autoload -U zargs

typeset PYTHON_INTERP="${${1:-python}:c}"; shift >/dev/null 2>&1 || true
zargs -r -- ./.(TAGS|cscope.*)(#qnonOd)(#q.N:a) -- rm -v

typeset -a directories=(
    "${@}"
    "${(f)$("${${(%):-%x}:a:h}/pypath" "${PYTHON_INTERP}")}"
)

typeset -a files_to_tag=(
    "${(f):-$("${${(%):-%x}:a:h}/pyfiles" "${(@)directories:a}")}"
)

ctags-exuberant \
    -f./.TAGS \
    --totals=yes \
    --verbose=no \
    --languages=Python \
    "${(@)files_to_tag:A}" &

if command -v pycscope >/dev/null 2>&1; then
    pycscope -f ./.cscope.out "${(@)files_to_tag:A}" &
else
    cscope -q -R -v -b -c -k -f ./.cscope.out "${(@)files_to_tag:A}" &
fi
wait
