# Copyright 2018 Jamiel Almeida
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Function that checks .git/ directory of the current repo (if it exists) for a
# shim script of the current command, and executes, otherwise just do the
# normal git stuff

setopt local_options

setopt extended_glob
setopt pipe_fail

command -v git >/dev/null 2>&1 || return 1
local COMMAND="${${:-git}:c}"
local DOTGIT_DIR=( (../)#.git(#qN/:a) )

if [[ "${#}" -eq 0 ]]; then
    set -- status -sb
elif [[ -n "${DOTGIT_DIR:-}" ]]; then
    local CANDIDATE="${DOTGIT_DIR}/${1}"
    if [[ -x "${CANDIDATE}" ]]; then
        COMMAND="${CANDIDATE}"
        shift
    fi
fi

if [[ "${1:-}" == 'clone' ]]; then
    shift
    my-git-clone "${@}"
else
    ${COMMAND} "${@}"
fi
