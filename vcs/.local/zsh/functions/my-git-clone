# Copyright 2018 Jamiel Almeida
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

setopt local_options
setopt err_return
setopt xtrace

# If the previous to last is the repo url or folder, assign it and the dest
# folder. Otherwise try the last parameter as the repo spec and generate the
# dest.

local -a ARGS=( "${@}" )
local REPO_SRC=''
local REPO_DST=''
local CLONED=''

if [[ "${ARGS[-2]:-}" == *(:|@)*(.git|) ]] || \
   [[ -d "${ARGS[-2]:-}/.git" ]]; then
    REPO_SRC="${ARGS[-2]}"
    REPO_DST="${ARGS[-1]:a}"
    ARGS=( ${ARGS[1,-3]} )
elif [[ "${ARGS[-1]:-}" == *(:|@)*(.git|) ]] || \
   [[ -d "${ARGS[-1]:-}/.git" ]]; then
    REPO_SRC="${ARGS[-1]}"
    REPO_DST="${HOME}/Repos/${${${${REPO_SRC#*@}#*://}/://}%.git}"
    ARGS=( ${ARGS[1,-2]} )
else
    echo "Must porvide a repository url to clone" >&2
    return 1
fi

if [[ ! -d "${REPO_DST}" ]]; then
    git clone "${(@)ARGS}" "${REPO_SRC}" "${REPO_DST}"
    CLONED=yes
fi
pushd "${REPO_DST}"
[[ -n "${CLONED}" ]] || \
    git fetch -a
